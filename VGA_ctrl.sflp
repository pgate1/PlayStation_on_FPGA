
circuit VGA_ctrl
{
	instrin htiming; // 25MHz
	output h_sync, v_sync;
	output h_en, v_en;
	output en;
	instrout vblank_begin, hblank_begin, view;
	output h_count<10>, v_count<10>;
	reg_wr h_count_reg<10>, v_count_reg<10>;

	input iR<8>, iG<8>, iB<8>;
	output oR<8>, oG<8>, oB<8>;

	reg_ws h_sync_reg, v_sync_reg;
	reg_wr h_en_reg, v_en_reg;

	instrself vtiming;

	par{
		h_sync = h_sync_reg;
		v_sync = v_sync_reg;

		h_en = h_en_reg;
		v_en = v_en_reg;
		en = h_en & v_en;

		h_count = h_count_reg;
		v_count = v_count_reg;

		if(h_en & v_en){
			oR = iR; oG = iG; oB = iB;
		}
		else{
			oR = 0; oG = 0; oB = 0;
		}
	}

	instruct htiming par{
		reg_wr h_tim<10>;
		any{
			h_tim==0 : par{
				h_sync_reg := 0;
			}
			h_tim==96 : par{
				h_sync_reg := 1;
				vtiming();
			}
			h_tim==(96+48) : par{
				h_en_reg := 1;
			}
			h_tim==(96+48+640) : par{
				h_en_reg := 0;
				hblank_begin();
			}
		}

		// view(h_en_reg)でvramからreadして次のクロックで表示する
		if(v_en_reg & h_en_reg & (h_tim<0>==0b0)) view();

		if(h_tim==(800-1)) h_tim := 0;
		else h_tim++;

		if(h_tim==(96+48)) h_count_reg := 0;
		else h_count_reg++;
	}

	instruct vtiming par{
		reg_wr v_tim<10>;
		any{
			v_tim==0 : par{
				v_sync_reg := 0;
			}
			v_tim==2 : par{
				v_sync_reg := 1;
			}
			v_tim==(2+33) : par{
				v_en_reg := 1;
			}
			v_tim==(2+33+480) : par{
				v_en_reg := 0;
				vblank_begin();
			}
		}

		if(v_tim==(525-1)) v_tim := 0;
		else v_tim++;

		if(v_tim==(2+33)) v_count_reg := 0;
		else v_count_reg++;
	}
}
