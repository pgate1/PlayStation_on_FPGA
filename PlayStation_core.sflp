/*
	PSX PlayStation on FPGA feat. DE2-115
	Copyright (c)2017 pgate1
*/

//#define DEMO

%i "R3000A.h"
%i "Cache_IR.h"
%i "Timer.h"
%i "SoundProcessingUnit.h"
%i "GraphicProcessingUnit.h"
%i "MDEC.h"
%i "DMA_GPU.h"
%i "DMA_CDR.h"
%i "DMA_SPU.h"
%i "DMA_OTC.h"
%i "CDROM_controller.h"
%i "PAD_controller.h"
%i "Scratchpad.h"
#ifdef DEMO
%i "demo/demo_core.h"
#endif

circuit PlayStation_core
{
	instrin cpu_run;
	R3000A cpu;
	Cache_IR ir_cache;
	reg_wr cache_control<12>;

	output A<21>;
	input Din<128>;

	instrout WRAM_READ(A, Word);
	instrin wram_ack;

	instrout BIOS_READ(A, Word);
	instrin bios_ack;

	instrout WRAM_WRITE(A, Dout, Word);
	output Dout<32>;
	output Word<5>; // 1〜16 Byte

	instrin exe_set(header_adrs, header_data);
	input header_adrs<12>, header_data<8>;

	input key_in<16>;

	instrself map_ram, map_pio, map_scratchpad, map_io, map_bios, map_cctrl;
	sel mapped_Din<32>;

	reg_ws reset;

	reg cpu_A<32>, cpu_Dout<32>, cpu_Word<3>;
	reg_wr pio_reg<32>, io_reg<32>;

	Scratchpad scratch;

	reg_wr spuDmaWait<32>;
	reg_wr ramSize<16>;

	reg_wr IREG<16>, IMASK<16>, DmaDPCR<32>, DmaDICR<32>;
	reg_wr DmaMADR_0<32>, DmaBCR_0<32>, DmaCHCR_0<32>;
	reg_wr DmaMADR_1<32>, DmaBCR_1<32>, DmaCHCR_1<32>;
	reg_wr DmaMADR_2<32>, DmaBCR_2<32>, DmaCHCR_2<32>;
	reg_wr DmaMADR_3<32>, DmaBCR_3<32>, DmaCHCR_3<32>;
	reg_wr DmaMADR_4<32>, DmaBCR_4<32>, DmaCHCR_4<32>;
	reg_wr DmaMADR_6<32>, DmaBCR_6<32>, DmaCHCR_6<32>;

	instrin run33;
	Timer timer_0, timer_1, timer_2, timer_3;

	SoundProcessingUnit spu;
	instrout spu_mem_write_req, spu_mem_read_req;
	output spu_mem_A<18>, spu_mem_Din<16>;
	instrin spu_mem_ack;
	input spu_mem_Dout<16>;
	instrin sw_intpl_off, sw_reverb_off;
	output voice_L<21>, voice_R<21>;
	output disp_env<24>;

	MDEC mdec;
	DMA_GPU dma_GPU;
	DMA_CDR dma_CDR;
	DMA_SPU dma_SPU;
	DMA_OTC dma_OTC;

	GraphicProcessingUnit gpu;
	instrin sw_outview_on;
	instrout view_on;
	output view_x<10>, view_y<9>;
	output view_R<8>, view_G<8>, view_B<8>;
	instrout fill_view_on;
	output dispW, dispH;
	instrout display_flip, draw_notdisp_side;

	output vram_adrs<19>, vram_write_data<16>, vram_burst<3>;
	instrout vram_write_req(vram_adrs, vram_write_data);
	instrout vram_read_req(vram_adrs, vram_burst);
	instrin vram_read_ack, vram_write_ack;
	input vram_read_data<64>;
	instrout vram_flush;
	instrin sw_texcache_off;

	instrin sw_kernel_run;
	instrin vblank_begin, vblank_end;
	instrin sw_cache_off;
	instrin sw_spu_off, sw_spuenv_off;
	instrin stop;
	instrin sw_view_half, sw_texture_off;

	CDROM_controller cdr;
	output file_adrs<32>;
	instrout file_read_req, file_preload;
	output file_adrs<32>;
	instrin file_ack;
	input file_data<8>, file_error;

	PAD_controller pad;
#ifdef DEMO
	demo_core demo;
#endif

	input sw_cdrom_ctrl<2>;

	instrout kernel_ok;

	instrself halt(h_code);
	sel h_code<8>;
	reg_wr halt_code_reg<8>;
	output halt_code<8>, halt_part<9>;

	output pc_out<32>, opcode_out<32>;

input dbg_in<16>;
output dbg<32>, dbg2<32>;
reg_wr dbg_reg<32>;
output dbg_cpu<8>;
output drawing;
output vraming;
output dma_running;
output dbg_cdr<32>, dbg_gpu<32>;
output dbg16<16>;
output dbg32<32>;
//reg_wr dbg32_reg<32>;
output dbg_pad<8>;

	stage_name ir_cache_read { task do(cpu_A); }
//	stage_name ir_cache_write { task do(cpu_A, cpu_Dout); }
//	stage_name ir_cache_flag_clear { task do(cache_A); }
	stage_name cpu_read { task do(cpu_A, cpu_Word); }
	stage_name cpu_write { task do(cpu_A, cpu_Dout, cpu_Word); }
	stage_name read_wram_wait { task do(); }
	stage_name read_bios_wait { task do(); }
	stage_name read_gpu_wait { task do(); }
	stage_name read_cdr_wait { task do(); }
	stage_name read_io_wait { task do(); }
	stage_name write_wram_wait { task do(); }

	if(reset){
		ir_cache.clear_all();
		reset := 0b0;
	}

	if(0b0){
		timer_3.io_set(0,0); timer_3.io_get(0);
	}

dbg = dbg_reg;
//dbg16 = cpu.dbg16;
dbg16 = pad.dbg16;
dbg32 = cdr.dbg32;//dbg32_reg;dma_CDR.dbg32;cpu.dbg32;

	instruct exe_set cpu.exe_set(header_adrs, header_data);

	sel peripheral_running;
	peripheral_running =
		(dma_GPU.running /*& (gpu.ref_dma_direction!=0b00) & ^gpu.drawing*/) |
		(/*(gpu.ref_dma_direction==0b00) &*/ gpu.drawing) | gpu.vram_busy |
		dma_CDR.running | dma_SPU.running | dma_OTC.running |
		cdr.running | pad.running |
		mdec.running |
		ir_cache.doing | ir_cache_read.do /*| ir_cache_write.do*/ | cpu_read.do | cpu_write.do |
		read_wram_wait.do | read_bios_wait.do | read_io_wait.do |
		read_gpu_wait.do | read_cdr_wait.do |
		write_wram_wait.do;

	instruct cpu_run par{
		alt{
			(halt_code!=0x00) | peripheral_running : ; // wait
			else : par{
				cpu.run();
			}
		}
	}

	// for PS-X EXE
	if(sw_cdrom_ctrl==0b01){
		reg_wr exe_running;
	//	if(cpu_read.do & (cpu_A==0x80030000) & ^exe_running){
		if(cpu.read_req & (cpu.A==0x80030000) & ^exe_running){ // キャッシュ先読み時
			exe_running := 0b1;
			kernel_ok();
		//	ir_cache.clear_all();
		}
	}

//cpu.iwait_max = dbg_in<7:0>;

	any{
		(cpu_A<31:24>==0x00) | (cpu_A<31:24>==0x80) | (cpu_A<31:24>==0xA0) : map_ram();
		cpu_A<31:20>==0x1F0 : map_pio();
		cpu_A<31:20>==0x1F8 : any{ // 0x1F80
			cpu_A<15:12>==0b0000 : map_scratchpad(); // <= 0x03FF(0x0FFF)
			else : map_io();
		}
		(cpu_A<31:20>==0x1FC) | (cpu_A<31:20>==0xBFC) : map_bios();
		cpu_A<31:16>==0xFFFE : map_cctrl();
		else : par{
//dbg_reg := cpu_A; halt(0x1E);
		}
	}

	any{
		map_ram : alt{
			ir_cache_read.do & ir_cache.hit : mapped_Din = ir_cache.dout;
		//	^sw_cache_off & cpu.is_c : mapped_Din = 0x00000000; // 未定義
			else : mapped_Din = Din<31:0>;
		}
		map_pio : mapped_Din = pio_reg;
		map_scratchpad : mapped_Din = scratch.Dout;
		map_io : mapped_Din = io_reg;
		map_bios : mapped_Din = Din<31:0>;
		else : mapped_Din = 0x00000000;
	}
	cpu.Din = mapped_Din;

	cpu.IRQ_in = IREG & IMASK;

	par(i=0; i<4; i++){
		timer[i].index = i;
	}

	spu_mem_A = spu.mem_A;
	spu_mem_Din = spu.mem_Din;
	instruct spu.mem_write_req spu_mem_write_req();
	instruct spu.mem_read_req spu_mem_read_req();
	instruct spu_mem_ack spu.mem_ack();
	spu.mem_Dout = spu_mem_Dout;
if(spu.halt_code!=0) dbg_reg := spu.dbg;
	instruct sw_intpl_off spu.sw_intpl_off();
	instruct sw_reverb_off spu.sw_reverb_off();

	if(sw_spu_off){
		voice_L = 0;
		voice_R = 0;
		spu.sw_spu_off();
		spu.sw_reverb_off();
	}
	else{
		voice_L = spu.voice_out_L;
		voice_R = spu.voice_out_R;
	}

	if(sw_spuenv_off){
		disp_env = 0;
		spu.sw_spuenv_off();
	}
	else{
		disp_env = spu.disp_env;
	}

#ifdef DEMO
	demo.voice_L = voice_L;
	demo.voice_R = voice_R;
#endif

	dma_GPU.DmaMADR_2 = DmaMADR_2;
	dma_GPU.DmaBCR_2 = DmaBCR_2;
	instruct dma_GPU.wram_read WRAM_READ(dma_GPU.A, 4);
	if(wram_ack & (gpu.halt_code==0)) dma_GPU.wram_ack();
	if(gpu.read_ack){
		dma_GPU.gpu_vram_ack();
		dma_GPU.Din = gpu.dout;
	}
	else{
		dma_GPU.Din = Din<31:0>;
	}
	instruct dma_GPU.gpu_dma_write_req if(gpu.halt_code==0x00) gpu.WriteData(dma_GPU.Dout);
	dma_GPU.gpu_busy = gpu.busy | (gpu.halt_code!=0);
//	dma_GPU.gpu_drawing = gpu.drawing;
	instruct dma_GPU.gpu_dma_read_req gpu.ReadData();
	gpu.busy_recieve_dmablock = dma_GPU.running;
	instruct dma_GPU.wram_write par{
		WRAM_WRITE(dma_GPU.A, dma_GPU.Dout, 4);
	}
	instruct dma_GPU.interrupt par{
		// gpuInterrupt()
		DmaCHCR_2 &= ^0x01000000;
		if(DmaDICR<18>){
			DmaDICR |= 0x04000000;
			IREG |= 0x0008;
		}
	}
//dma_GPU.dbg_in = dbg_in;
//dbg = dma_GPU.dbg;
dma_running = dma_GPU.running;

	dma_CDR.DmaMADR_3 = DmaMADR_3;
	dma_CDR.DmaBCR_3 = DmaBCR_3;
	dma_CDR.DmaCHCR_3 = DmaCHCR_3;
	instruct dma_CDR.dma_read_req cdr.dma_read_req();
	dma_CDR.dma_read_data = cdr.dma_read_Dout;
	if(wram_ack) dma_CDR.wram_ack();
	instruct dma_CDR.wram_write par{
		WRAM_WRITE(dma_CDR.A, dma_CDR.Dout, 4);
	}
	instruct dma_CDR.interrupt par{
		// cdrDmaInterrupt()
		if(DmaCHCR_3<24>){
			DmaCHCR_3 &= ^0x01000000;
			if(DmaDICR<19>){
				DmaDICR |= 0x08000000;
				IREG |= 0x0008;
			}
		}
	}

	dma_SPU.DmaMADR_4 = DmaMADR_4;
	dma_SPU.DmaBCR_4 = DmaBCR_4;
	instruct dma_SPU.wram_read WRAM_READ(dma_SPU.A, 4);
	instruct wram_ack dma_SPU.wram_ack();
	dma_SPU.Din = Din<31:0>;
	instruct dma_SPU.spu_dma_write_req spu.dma_write_req(dma_SPU.Dout);
	instruct spu.dma_ack dma_SPU.spu_dma_ack();
	spu.dma_doing = dma_SPU.running;
	instruct dma_SPU.interrupt par{
		DmaCHCR_4 &= ^0x01000000;
		if(DmaDICR<20>){
			DmaDICR |= 0x10000000;
			IREG |= 0x0008;
		}
	}

	dma_OTC.DmaMADR_6 = DmaMADR_6;
	dma_OTC.DmaBCR_6 = DmaBCR_6;
	instruct dma_OTC.wram_write par{
		WRAM_WRITE(dma_OTC.A, dma_OTC.D, 4);
	}
	instruct wram_ack dma_OTC.wram_ack();
	instruct dma_OTC.interrupt par{
		DmaCHCR_6 &= ^0x01000000;
		if(DmaDICR<22>){
			DmaDICR |= 0x40000000;
			IREG |= 0x0008;
		}
	}
//dma_OTC.dbg_in = dbg_in;
//dbg = dma_OTC.dbg;

	instruct cdr.interrupt_out par{
		IREG |= 0x0004;
	}
	instruct cdr.interrupt_out_stopcdda par{
		IREG |= 0x0200;
	}
	instruct cdr.file_read_req file_read_req();
	instruct cdr.file_preload file_preload();
	file_adrs = cdr.file_adrs;
	instruct file_ack cdr.file_ack();
	cdr.file_data = file_data;
	cdr.file_error = file_error;
	cdr.sw_cdrom_ctrl = sw_cdrom_ctrl;
//cdr.dbg_in = dbg_in;
if(cdr.halt_code!=0) dbg_reg := cdr.dbg;
dbg_cdr = cdr.dbg_irq;

	pad.key_in = key_in;
	instruct pad.interrupt_out par{
		IREG |= 0x0080;
	}
	instruct pad.interrupt_off par{
		IREG &= ^0x0080;
	}

	mdec.DmaMADR_0 = DmaMADR_0;
//	mdec.DmaBCR_0 = DmaBCR_0;
	instruct mdec.wram_write WRAM_WRITE(mdec.wram_adrs, mdec.wram_din, 4);
	instruct wram_ack mdec.wram_ack();
	instruct mdec.mdec0_interrupt par{
		// mdec0Interrupt()
		DmaCHCR_0 &= ^0x01000000;
		if(DmaDICR<16>){
			DmaDICR |= 0x01000000;
			IREG |= 0x0008;
		}
	}
	instruct mdec.end_of_data par{
		DmaCHCR_0 &= ^0x01000000;
		if(DmaDICR<16>){
			DmaDICR |= 0x01000000;
			IREG |= 0x0008;
		}
	}
	instruct mdec.mdec1_interrupt par{
		// mdec1Interrupt()
		DmaCHCR_1 &= ^0x01000000;
		if(DmaDICR<17>){
			DmaDICR |= 0x02000000;
			IREG |= 0x0008;
		}
	}

#ifdef DEMO
	instruct demo.gpu_write_req gpu.WriteData(demo.Dout);
	demo.drawing = gpu.drawing;
#endif

	if(/|timer_0.halt_code | /|timer_1.halt_code |
	   /|timer_2.halt_code /*| /|timer_3.halt_code*/) halt(0x40);

	pc_out = cpu.pc_out;
	opcode_out = cpu.opcode_out;
	halt_code = halt_code_reg | cpu.halt_code | spu.halt_code | gpu.halt_code
				| dma_CDR.halt_code | dma_GPU.halt_code | dma_OTC.halt_code | cdr.halt_code | pad.halt_code;
	halt_part = /|halt_code_reg || /|cpu.halt_code || /|spu.halt_code || /|gpu.halt_code
				|| /|dma_CDR.halt_code || /|dma_GPU.halt_code || /|dma_OTC.halt_code || /|cdr.halt_code || /|pad.halt_code;

	instruct halt halt_code_reg := h_code;
if(cpu.halt_code!=0) dbg_reg := 0x000000 || cpu.dbg;
drawing = gpu.drawing;
vraming = gpu.vram_busy;

	instruct cpu.read_req par{
	//	generate cpu_read.do(cpu.A, cpu.Word);

		// アクセスを速くするためここでキャッシュチェック
		if((^sw_cache_off) & ((cpu.A<31:24>==0x00) | (cpu.A<31:24>==0x80)) & cpu.ir_read){
			// 命令フェッチのみ対象
			ir_cache.read(cpu.A<20:0>);
			generate ir_cache_read.do(cpu.A);
		}
		else{
			generate cpu_read.do(cpu.A, cpu.Word);
		}

//if(cpu.ir_read) opcount++;
	}

	instruct cpu.write_req par{
		generate cpu_write.do(cpu.A, cpu.Dout, cpu.Word);
/*
		if((^sw_cache_off) & ((cpu.A<31:24>==0x00) | (cpu.A<31:24>==0x80)) & cpu.is_c){
		//	ir_cache.write(cpu.A<20:0>, cpu.Dout);
			ir_cache.clear(cpu.A<20:0>);
		}
		else{
			generate cpu_write.do(cpu.A, cpu.Dout, cpu.Word);
		}
*/
	}
/*
	// キャッシュ分離解除時にクリア？
	// BIOSのループでクリアするから回路は不要そう？
	reg_wr is_c_old;
	is_c_old := cpu.is_c;
	if(^cpu.is_c & is_c_old) ir_cache.clear_all();
*/
	alt{
		halt_code!=0x00 : ;
		sw_kernel_run : par{

			if(run33){ // 33.8688MHz
				spu.run33();
			}

			if(cpu.run){
				par(i=0;i<4;i++){
					timer[i].run33();
				}
			}

			if(cpu.run){
				mdec.run33();
				dma_GPU.run33();
				dma_CDR.run33();
				dma_SPU.run33();
				dma_OTC.run33();
				cdr.run33();
				pad.run33(); // CPUに合わせてキー入力できるようになった
			}

		}
		else : par{ // PS-X EXE 実行
			instruct run33 par{ // 33.8688MHz
				spu.run33();
				par(i=0;i<4;i++){
					timer[i].run33();
				}
			}
			if(cpu.run){
				mdec.run33();
				dma_GPU.run33();
				dma_CDR.run33();
				dma_SPU.run33();
				dma_OTC.run33();
				cdr.run33();
				pad.run33();
			}
		}
	}

	instruct timer_0.irq_out par{
		IREG |= 0x00 || timer_0.irq_code;
	}
	instruct timer_1.irq_out par{
		IREG |= 0x00 || timer_1.irq_code;
	}
	instruct timer_2.irq_out par{
		IREG |= 0x00 || timer_2.irq_code;
	}
//	instruct timer_3.irq_out par{
//		if(^sw_kernel_run) IREG |= 0x00 || timer_3.irq_code;
//	}

	instruct vblank_begin par{
	//	if(sw_kernel_run){
			//if(^dma_GPU.running) 
			IREG |= 0x0001;
	//	}
		gpu.vblank_begin();
#ifdef DEMO
		if(^sw_kernel_run) demo.vblank();
#endif
	}
	instruct vblank_end gpu.vblank_end();
	instruct timer_3.hsync gpu.hsync();

	reg_wr cdr_dma_count<4>, f_texture_full, f_view_half;
//	if(cdr_dma_count==8) f_texture_full := 0b1;
	if(/&cdr_dma_count) f_view_half := 0b1;
//	if(f_texture_full) 
gpu.texture_full();
	if(f_view_half) gpu.sw_view_half();
//if(cdr_dma_count!=0) dbg_reg := 0x0000000||cdr_dma_count;

gpu.dbg_in = dbg_in;
	if(sw_kernel_run) gpu.sw_kernel_run();
	instruct gpu.view_on view_on();
	view_x = gpu.view_x;
	view_y = gpu.view_y;
	view_R = gpu.view_R;
	view_G = gpu.view_G;
	view_B = gpu.view_B;
	instruct gpu.fill_view_on fill_view_on();
	dispW = gpu.dispW;
	dispH = gpu.dispH;
	instruct gpu.draw_notdisp_side draw_notdisp_side();
if(gpu.halt_code!=0) dbg_reg := gpu.dbg;

	instruct sw_outview_on gpu.sw_outview_on();
//	instruct sw_view_half gpu.sw_view_half();
	instruct sw_texture_off gpu.sw_texture_off();

	instruct gpu.vram_write_req par{
		vram_write_req(gpu.vram_adrs, gpu.vram_write_data);
	}
	instruct gpu.vram_read_req par{
		vram_read_req(gpu.vram_adrs, gpu.vram_burst);
	}
	instruct vram_write_ack gpu.vram_write_ack();
	instruct vram_read_ack gpu.vram_read_ack();
	gpu.vram_read_data = vram_read_data;
	instruct gpu.display_flip display_flip();
	instruct gpu.vram_flush_req vram_flush();
	instruct sw_texcache_off gpu.sw_texcache_off();
dbg_gpu = gpu.dbg_gpu;

	// ir_readの時に起動
	stage ir_cache_read {
		first_state st_check;
		state st_check par{
			if(ir_cache.hit){
				cpu.read_ack();
				finish;
			}
			else{
				if(cpu.is_c){ // 未定義
					cpu.read_ack(); // とりあえずack返す
					finish;
				}
				else{
					sel offset<2>;
					offset = cpu_A<3:2>;
					switch(offset){
						case 0: WRAM_READ(cpu_A<20:0>, 16);
						case 1: WRAM_READ(cpu_A<20:0>, 12);
						case 2: WRAM_READ(cpu_A<20:0>, 8);
						case 3: WRAM_READ(cpu_A<20:0>, 4);
					}
					generate read_wram_wait.do();
					goto st_write;
				}
			}
		}
		state st_write if(read_wram_wait.do & wram_ack){
			// ここでDinはoffsetが反映されたものになっている
			ir_cache.write_line(cpu_A<20:0>, Din);
			goto st_check;
			finish;
		}
	}

	stage cpu_read {
		par{
			any{
				map_ram : par{

					WRAM_READ(cpu_A<20:0>, 0b00||cpu_Word);
					generate read_wram_wait.do();
/*
					if((^sw_cache_off) & ((cpu_A<31:24>==0x00) | (cpu_A<31:24>==0x80)) & cpu.ir_read){
						// 命令フェッチのみ対象
						ir_cache.read(cpu_A<20:0>);
						generate ir_cache_read.do(cpu_A);
					}
					else{
						WRAM_READ(cpu_A<20:0>, cpu_Word);
						generate read_wram_wait.do();
					}
*/
				}
				map_pio : par{
					pio_reg := 0x00000000;
					generate read_io_wait.do();
				}
				map_scratchpad : par{ // Scratchpad 0x0000-0x03FF(0x0FFF)
					//if(cpu_A<11:10>==0b00) 
						scratch.read(cpu_A<9:0>);
					//else{dbg_reg := cpu_A;halt(0x58);}
					generate read_io_wait.do();
				}
				map_io : par{ // 0x1F80
					any{
						cpu_A<15:0>==0x1014 : io_reg := spuDmaWait;
					//	cpu_A<15:0>==0x1030 : ; // ?
						cpu_A<15:0>==0x1040 : par{ // パッドデータ
						//	io_reg := 0x000000 || pad.ReadData().io_dout<7:0>;
							pad.ReadData();
							io_reg := pad.io_dout<7:0> || pad.io_dout<7:0> || pad.io_dout<7:0> || pad.io_dout<7:0>;
						}
						cpu_A<15:0>==0x1044 : par{ // パッドステータス
						//	io_reg := 0x0000 || pad.ReadStat().io_dout;
							pad.ReadStat();
							io_reg := pad.io_dout || pad.io_dout;
						}
						cpu_A<15:0>==0x104A : io_reg := pad.ReadCtrl().io_dout || 0x0000; // パッドコントロール
						cpu_A<15:0>==0x104E : io_reg := pad.ReadBaud().io_dout || 0x0000; // パッドボーレート
						cpu_A<15:0>==0x1054 : io_reg := 0; // test
						cpu_A<15:0>==0x1058 : io_reg := 0; // test
						cpu_A<15:0>==0x105A : io_reg := 0; // test
						cpu_A<15:0>==0x105C : io_reg := 0; // test
						cpu_A<15:0>==0x1060 : io_reg := 0x0000 || ramSize;
						cpu_A<15:0>==0x1070 : io_reg := 0x0000 || IREG;
						cpu_A<15:0>==0x1074 : io_reg := 0x0000 || IMASK;

						cpu_A<15:0>==0x1080 : io_reg := DmaMADR_0; // DMACチャネル0(MDECin)メモリアドレス
						cpu_A<15:0>==0x1088 : io_reg := DmaCHCR_0; // DMACチャネル0(MDECin)チャネルコントロール
						cpu_A<15:0>==0x1090 : io_reg := DmaMADR_1; // DMACチャネル1(MDECout)メモリアドレス
						cpu_A<15:0>==0x1094 : io_reg := DmaBCR_1;  // DMACチャネル1(MDECout)ブロックカウント
						cpu_A<15:0>==0x1098 : io_reg := DmaCHCR_1; // DMACチャネル1(MDECout)チャネルコントロール
						cpu_A<15:0>==0x10A0 : io_reg := DmaMADR_2; // DMACチャネル2(GPU)メモリアドレス
						cpu_A<15:0>==0x10A8 : io_reg := DmaCHCR_2; // DMACチャネル2(GPU)チャネルコントロール
						cpu_A<15:0>==0x10B8 : io_reg := DmaCHCR_3; // DMACチャネル3(CD)チャネルコントロール
						cpu_A<15:0>==0x10C8 : io_reg := DmaCHCR_4; // DMACチャネル4(SPU)チャネルコントロール
						cpu_A<15:0>==0x10E8 : io_reg := DmaCHCR_6; // DMACチャネル6(OTC)チャネルコントロール

						cpu_A<15:0>==0x10F0 : io_reg := DmaDPCR;
						cpu_A<15:0>==0x10F4 : io_reg := DmaDICR;
						cpu_A<15:0>==0x10F6 : io_reg := 0; // unknown
						cpu_A<15:8>==0x11 : any{ // 0x11--
							cpu_A<7:4>==0x0 : io_reg := 0x0000 || timer_0.io_get(cpu_A<3:0>).io_Dout;
							cpu_A<7:4>==0x1 : io_reg := 0x0000 || timer_1.io_get(cpu_A<3:0>).io_Dout;
							cpu_A<7:4>==0x2 : io_reg := 0x0000 || timer_2.io_get(cpu_A<3:0>).io_Dout;
						//	cpu_A<7:4>==0x3 : ;//io_reg := timer_3.io_get(cpu_A<3:0>).io_Dout;
							else : halt(0x54);
						}
						cpu_A<15:4>==0x180 : par{ // CDROMコントローラレジスタ
							cdr.Read(cpu_A<1:0>);
							generate read_cdr_wait.do();
						}
						cpu_A<15:0>==0x1810 : par{ // GPUデータ／コマンド
							gpu.ReadData();
							generate read_gpu_wait.do();
						}
						cpu_A<15:0>==0x1814 : par{ // GPUステータス／コントロール
							io_reg := gpu.read_status().dout;
						}
						cpu_A<15:0>==0x1820 : io_reg := mdec.ReadData().dout; // MDEC Data/Response Register (R)
						cpu_A<15:0>==0x1824 : io_reg := mdec.ReadStatus().dout; // MDEC Status Register (R)

						cpu_A<15:10>==0b000111 : par{ // 0x1C00-1E00
							io_reg := spu.io_get(cpu_A<15:0>).io_Dout || spu.io_Dout;
						}

						else : par{
							dbg_reg := cpu_A;
							halt(0x57);
						}
					}
					generate read_io_wait.do();
				}
				map_bios : par{
					BIOS_READ(0b00||cpu_A<18:0>, 0b00||cpu_Word);
					generate read_bios_wait.do();
				}
				map_cctrl : if(cpu_A<15:0>==0x0130){
					io_reg := 20#0b0 || cache_control;
					generate read_io_wait.do();
				}
				else : read_io_wait.do();
			}
			finish;
		}
	}

	stage read_wram_wait {
		if(wram_ack){
			cpu.read_ack();
			finish;
		}
	}

	stage read_io_wait {
		any{
			read_gpu_wait.do : finish;
			read_cdr_wait.do : finish;
			else : par{
				cpu.read_ack();
				finish;
			}
		}
	}

	stage read_gpu_wait {
		first_state st1;
		state st1 if(gpu.read_ack){
			io_reg := gpu.dout;
			goto st2;
		}
		state st2 par{
			cpu.read_ack();
			goto st1;
			finish;
		}
	}

	stage read_cdr_wait {
		first_state st1;
		state st1 par{
			io_reg := cdr.io_dout || cdr.io_dout || cdr.io_dout || cdr.io_dout; // ok
		//	io_reg := 0x000000 || cdr.io_dout; // NG
			goto st2;
		}
		state st2 par{
			cpu.read_ack();
			goto st1;
			finish;
		}
	}

	stage read_bios_wait {
		if(bios_ack){
			cpu.read_ack();
			finish;
		}
	}

	stage cpu_write {
		par{
			any{
				map_ram : par{
				//	WRAM_WRITE(cpu_A<20:0>, cpu_Dout, cpu_Word);
				//	generate write_wram_wait.do();

					if((^sw_cache_off) & cpu.is_c){// & ((cpu_A<31:24>==0x00) | (cpu_A<31:24>==0x80))){
						ir_cache.clear(cpu_A<20:0>);
					//	ir_cache.write(cpu_A<20:0>, cpu_Dout);
					}
					else{
						WRAM_WRITE(cpu_A<20:0>, cpu_Dout, 0b00||cpu_Word);
						generate write_wram_wait.do();
					//	ir_cache.clear(cpu_A<20:0>); // いずれも不要
					}

				}
				map_pio : par{
					halt(0x53);
				}
				map_scratchpad : par{ // Scratchpad 0x0000-0x03FF
					//if(cpu_A<11:10>==0b00) 
						scratch.write(cpu_A<9:0>, cpu_Dout, cpu_Word);
					//else{dbg_reg := cpu_A;halt(0x56);}
				}
				map_io : any{
					cpu_A<15:0>==0x1000 : ;
					cpu_A<15:0>==0x1004 : ;
					cpu_A<15:0>==0x1008 : ;
					cpu_A<15:0>==0x100C : ;
					cpu_A<15:0>==0x1010 : ;
					cpu_A<15:0>==0x1014 : spuDmaWait := cpu_Dout;
					cpu_A<15:0>==0x1018 : ;
					cpu_A<15:0>==0x101C : ;
					cpu_A<15:0>==0x1020 : ;
					cpu_A<15:0>==0x1040 : pad.WriteData(0x00||cpu_Dout<7:0>); // パッドデータ
					cpu_A<15:0>==0x1048 : pad.WriteMode(cpu_Dout<15:0>); // パッドモード
					cpu_A<15:0>==0x104A : pad.WriteCtrl(cpu_Dout<15:0>); // パッドコントロール
					cpu_A<15:0>==0x104C : ;
					cpu_A<15:0>==0x104E : pad.WriteBaud(cpu_Dout<15:0>); // パッドボーレート
					cpu_A<15:0>==0x1058 : ; // test
					cpu_A<15:0>==0x105A : ; // test
					cpu_A<15:0>==0x105C : ; // test
					cpu_A<15:0>==0x105E : ; // test

					cpu_A<15:0>==0x1060 : ramSize := cpu_Dout<15:0>;
					cpu_A<15:0>==0x1070 : par{ // 割り込みステータス
						IREG &= cpu_Dout<15:0>;
					}
					cpu_A<15:0>==0x1074 : IMASK := cpu_Dout<15:0>; // 割り込みマスク

					cpu_A<15:0>==0x1080 : DmaMADR_0 := cpu_Dout; // DMACチャネル0(MDECin)メモリアドレス
					cpu_A<15:0>==0x1084 : DmaBCR_0  := cpu_Dout; // DMACチャネル0(MDECin)ブロックカウント
					cpu_A<15:0>==0x1088 : par{ // DMACチャネル0(MDECin)チャネルコントロール
						DmaCHCR_0 := cpu_Dout;
						if(DmaDPCR<3> & (cpu_Dout==0x01000201)){
							mdec.DMAin_Execute();
						}
					}
					cpu_A<15:0>==0x1090 : DmaMADR_1 := cpu_Dout; // DMACチャネル1(MDECout)メモリアドレス
					cpu_A<15:0>==0x1094 : DmaBCR_1  := cpu_Dout; // DMACチャネル1(MDECout)ブロックカウント
					cpu_A<15:0>==0x1098 : par{ // DMACチャネル1(MDECout)チャネルコントロール
						DmaCHCR_1 := cpu_Dout;
						if(DmaDPCR<7> & (cpu_Dout==0x01000200)){
							mdec.DMAout_Execute();
						}
					}
					cpu_A<15:0>==0x10A0 : DmaMADR_2 := cpu_Dout; // DMACチャネル2(GPU)メモリアドレス
					cpu_A<15:0>==0x10A4 : DmaBCR_2  := cpu_Dout; // DMACチャネル2(GPU)ブロックカウント
					cpu_A<15:0>==0x10A8 : par{ // DMACチャネル2(GPU)チャネルコントロール
						DmaCHCR_2 := cpu_Dout;
						if(DmaDPCR<11>) any{
							cpu_Dout==0x01000200 : par{
								dma_GPU.Execute_vram2mem();
							}
							cpu_Dout==0x01000201 : par{
								dma_GPU.Execute_mem2vram();
							}
							cpu_Dout==0x01000401 : par{
								dma_GPU.Execute_chain();
							}
						}
//if(dma_GPU.Execute_mem2vram & dma_GPU.Execute_chain) halt(0x2F);
					}

					cpu_A<15:0>==0x10B0 : DmaMADR_3 := cpu_Dout;
					cpu_A<15:0>==0x10B4 : DmaBCR_3 := cpu_Dout;
					cpu_A<15:0>==0x10B8 : par{
						DmaCHCR_3 := cpu_Dout;
						if(DmaDPCR<15> & ((cpu_Dout==0x11000000) | (cpu_Dout==0x11400100))){
							dma_CDR.Execute();
							cdr_dma_count++; // test
						}
					}

					cpu_A<15:0>==0x10C0 : DmaMADR_4 := cpu_Dout;
					cpu_A<15:0>==0x10C4 : DmaBCR_4 := cpu_Dout;
					cpu_A<15:0>==0x10C8 : par{
						DmaCHCR_4 := cpu_Dout;
						if(DmaDPCR<19> & (cpu_Dout==0x01000201)){ // mem2spuram
							dma_SPU.Execute();
						}
					}
					cpu_A<15:0>==0x10E0 : DmaMADR_6 := cpu_Dout; // DMACチャネル6(OTC)メモリアドレス
					cpu_A<15:0>==0x10E4 : DmaBCR_6 := cpu_Dout; // DMACチャネル6(OTC)ブロックカウント
					cpu_A<15:0>==0x10E8 : par{ // DMACチャネル6(OTC)チャネルコントロール
						DmaCHCR_6 := cpu_Dout;
						// 0b1000_0000_0000_0000_0000_0000_0000
						if(DmaDPCR<27> & (cpu_Dout==0x11000002)){
							dma_OTC.Execute();
						}
					}

					cpu_A<15:0>==0x10F0 : DmaDPCR |= cpu_Dout; // DMAC優先度コントロール
					cpu_A<15:0>==0x10F4 : par{ // DMAC割り込みコントロール
						sel tmp<32>;
						tmp = (^cpu_Dout) & DmaDICR;
						DmaDICR := ((tmp @ cpu_Dout) & 0x00FFFFFF) @ tmp;
					}
					cpu_A<15:0>==0x10F6 : ; // unknown
					cpu_A<15:8>==0x11 : any{ // 0x11--
						cpu_A<7:4>==0x0 : timer_0.io_set(cpu_A<3:0>, cpu_Dout<15:0>);
						cpu_A<7:4>==0x1 : timer_1.io_set(cpu_A<3:0>, cpu_Dout<15:0>);
						cpu_A<7:4>==0x2 : timer_2.io_set(cpu_A<3:0>, cpu_Dout<15:0>);
					//	cpu_A<7:4>==0x3 : ;//timer_3.io_set(cpu_A<3:0>, cpu_Dout<15:0>);
						else : halt(0x54);
					}
					cpu_A<15:4>==0x180 : par{ // CDROMコントローラレジスタ
						cdr.Write(cpu_A<1:0>, cpu_Dout<7:0>);
					}
					cpu_A<15:0>==0x1810 : par{ // GPUデータ／コマンド
						gpu.WriteData(cpu_Dout);
//if(dma_GPU.gpu_dma_write_req) halt(0x94);
					}
					cpu_A<15:0>==0x1814 : par{ // GPUステータス／コントロール
						gpu.write_status(cpu_Dout);
					}
					cpu_A<15:0>==0x1820 : mdec.WriteCmd(cpu_Dout); // MDEC Command/Parameter Register (W)
					cpu_A<15:0>==0x1824 : mdec.WriteCtrl(cpu_Dout); // MDEC Control/Reset Register (W)
					cpu_A<15:10>==0b000111 : par{ // 0x1F80_1C00-1E00
						spu.io_set(cpu_A<15:0>, cpu_Dout<15:0>);
					}
					cpu_A<15:0>==0x2041 : ; // ディップスイッチ?
					else : par{
						dbg_reg := cpu_A;
						halt(0x55);
					}
				}
				map_cctrl : if(cpu_A<15:0>==0x0130){
					cache_control := cpu_Dout<11:0>;
					if((cpu_Dout==0x0001E988)/*(cpu_Dout==0x00000800) | (cpu_Dout==0x00000804)*/){
						ir_cache.clear_all();
					}
				}
				//else : halt(0xDE);
			}
			finish;
		}
	}

	stage write_wram_wait {
		if(wram_ack){
			finish;
		}
	}
/*
	stage ir_cache_flag_clear {
		par{
			ir_cache.flag_clear(cache_A);
			finish;
		}
	}
*/
}
